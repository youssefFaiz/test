// @version=6
indicator("FVG indicator", overlay=true, max_bars_back = 1000, max_lines_count = 500, max_labels_count = 500, max_boxes_count = 500)

type General_Settings 
    bool    remove_filled
    string  mitigated_type
    bool    ltf_hide

type FVG_Settings
    bool    show
    string  htf
    color   color_bull
    color   color_bear
    int     max_count
    string  label_text

type FVG
    int     open_time
    int     close_time
    float   middle
    float   open
    float   close
    bool    mitigated
    int     mitigated_time
    string  labelText
    line    ce_line
    box     box

type FVG_Structure
    FVG[]           fvg
    FVG_Settings    settings

type Helper
    string name         = "Helper"

General_Settings settings     = General_Settings.new()
FVG_Settings HTF_1_Settings   = FVG_Settings.new()

htf_1                        = input.timeframe("1", "TF1", inline="htf1", group = "TIMEFRAMES")
HTF_1_Settings.htf          := htf_1
HTF_1_Settings.max_count    := 100
HTF_1_Settings.label_text   := input.string("1", "Label", inline="htf1", group = "TIMEFRAMES")
HTF_1_Settings.show         := input.bool(true, "", inline="htf1", group = "TIMEFRAMES")

settings.remove_filled      := input.bool(true, "Delete boxes after fill?", group = "FVG SETTINGS")
settings.mitigated_type     := "Close"
settings.ltf_hide           := input.bool(true, "Hide FVG's lowers than enabled timeframes?")

bull_box_color              = input.color(defval = color.new(color.green, 90), title = "Bullish FVG box color", group = "BOX VISUALS")
bear_box_color              = input.color(defval = color.new(color.red, 90), title = "Bearish FVG box color", group = "BOX VISUALS")

label_bull_color            = input.color(color.new(#000000, 0), "Bullish FVG labels color", group = "LABELS VISUALS")
label_bear_color            = input.color(color.new(#000000, 0), "Bearish FVG labels color", group = "LABELS VISUALS")
label_size                  = input.string(size.tiny, "Labels size", [size.tiny, size.small, size.normal, size.large, size.huge], group = "LABELS VISUALS")
label_position              = input.string(defval = text.align_right, title = "Labels position", options = [text.align_center, text.align_left, text.align_right], group = "LABELS VISUALS")

border_width                = input.int(1, title = "Border width", minval = 1, maxval = 3, group = "BORDER VISUALS")
bull_border_color           = input.color(defval = color.new(color.green, 100), title = "Bullish FVG border color", group = "BORDER VISUALS")
bear_border_color           = input.color(defval = color.new(color.red, 100), title = "Bearish FVG border color", group = "BORDER VISUALS")

bull_ce_color               = input.color(defval = #00000000, title = "C.E. bullish FVG line color", group = "C.E. VISUALS")
bear_ce_color               = input.color(defval = #00000000, title = "C.E. bearish FVG line color", group = "C.E. VISUALS")
ce_padding                  = input.int(2, title = "C.E. line padding", minval = 1, maxval = 50, group = "C.E. VISUALS", tooltip = "Space between C.E. line and label text")

Helper helper                       = Helper.new()

var FVG_Structure FVG_1_Structure        = FVG_Structure.new()

var array<FVG> FVGs_1              = array.new<FVG>()

FVG_1_Structure.fvg                     := FVGs_1
FVG_1_Structure.settings                := HTF_1_Settings

var int TF_1  = 0

method Validtimeframe(Helper helper, tf) =>
    helper.name := tf
    n1 = timeframe.in_seconds()
    n2 = timeframe.in_seconds(tf)
    n1 <= n2

method _AddBoxes(FVG_Structure FVG_Struct, FVG fvg) =>
    bool visible = true
    if FVG_Struct.settings.show
        // Extend to current bar time instead of fixed length
        buffer = time
        line_buffer = time - (time - time[1]) * ce_padding
        
        if na(fvg.box)
            // Create new box
            fvg.box := box.new(fvg.open_time, fvg.open, buffer, fvg.close, border_color = fvg.open < fvg.close ? bull_border_color : bear_border_color, border_width = border_width, bgcolor = fvg.open < fvg.close ? bull_box_color : bear_box_color, xloc=xloc.bar_time, text = fvg.labelText,
             text_color = fvg.open < fvg.close ? label_bull_color : label_bear_color, text_halign = label_position, text_size = label_size)
            fvg.ce_line := line.new(fvg.open_time, fvg.middle, line_buffer, fvg.middle, xloc = xloc.bar_time, style = line.style_dotted,
             color = fvg.open < fvg.close ? bull_ce_color : bear_ce_color)
        else
            // Update existing box to extend to current bar if not mitigated
            if not fvg.mitigated
                box.set_right(fvg.box, buffer)
                line.set_x2(fvg.ce_line, line_buffer)

        if fvg.mitigated and settings.remove_filled
            if not na(fvg.box)
                box.delete(fvg.box)
                line.delete(fvg.ce_line)
                visible := false
    visible

method _CreateFVG(FVG_Structure FVG_Struct, float _open, float _close, int opening_time, string lblText) =>
    FVG fvg = FVG.new()
    fvg.open_time           := opening_time
    fvg.open                := _open
    fvg.close               := _close
    fvg.middle              := (_open + _close) / 2
    fvg.labelText           := lblText
    FVG_Struct.fvg.unshift(fvg)
    if FVG_Struct.fvg.size() > 100
        temp = FVG_Struct.fvg.pop()
        if not na(temp.box)
            box.delete(temp.box)
            line.delete(temp.ce_line)
    FVG_Struct

method _CheckMitigatedFVGs(FVG_Structure FVG_Struct, o, h, l, c) =>
    if FVG_Struct.fvg.size() > 0
        for i = FVG_Struct.fvg.size() - 1 to 0
            fvg = FVG_Struct.fvg.get(i)
            if not fvg.mitigated
                switch settings.mitigated_type
                    'Close' =>
                        fvg.mitigated   := fvg.open < fvg.close ? c < fvg.open : c > fvg.open
                if fvg.mitigated
                    // Set the close time when mitigated
                    fvg.mitigated_time := time
                    // Update box to end at mitigation bar
                    if not na(fvg.box)
                        box.set_right(fvg.box, time)
                        line.set_x2(fvg.ce_line, time - (time - time[1]) * ce_padding)
                    
                    if settings.remove_filled
                        if not na(fvg.box)
                            box.delete(fvg.box)
                            line.delete(fvg.ce_line)
                        FVG_Struct.fvg.remove(i)
    FVG_Struct

method _DrawBoxes(FVG_Structure FVG_Struct, int step) =>
    int count = 0
    if FVG_Struct.fvg.size() > 0
        for i = 0 to FVG_Struct.fvg.size() - 1
            fvg = FVG_Struct.fvg.get(i)
            if count < FVG_Struct.settings.max_count
                if FVG_Struct._AddBoxes(fvg)
                    count := count+1
            else
                if not na(fvg.box)
                    box.delete(fvg.box)
                    line.delete(fvg.ce_line)
    FVG_Struct

method _FindFVGs(FVG_Structure FVG_Struct, o, h, l, c, t, o1, h1, l1, c1, t1, o2, h2, l2, c2, t2, labelText) =>
    if FVG_Struct.settings.show and (h < l2 or l > h2) 
        o = h < l2 ? l2 : h2
        c = h < l2 ? h : l
        if FVG_Struct.fvg.size() == 0 
            FVG_Struct._CreateFVG(o, c, t2, labelText)
        else
            if FVG_Struct.fvg.first().open_time < t2
                FVG_Struct._CreateFVG(o, c, t2, labelText)
    FVG_Struct

method _ProcessData(FVG_Structure FVG_Struct, float o, float h, float l, float c, int t, float o1, float h1, float l1, float c1, int t1, float o2, float h2, float l2, float c2, int t2, string labelText) =>
    var int show = 0
    if FVG_Struct.settings.show
        if not settings.ltf_hide or (settings.ltf_hide and helper.Validtimeframe(FVG_Struct.settings.htf))
            if FVG_Struct.settings.show
                FVG_Struct._FindFVGs(o, h, l, c, t, o1, h1, l1, c1, t1, o2, h2, l2, c2, t2, labelText)
                show := 1
        FVG_Struct._CheckMitigatedFVGs(o, h, l, c)
    show



//Main call to start processing the data according to HTFs defined in settings
if HTF_1_Settings.show
    [o_1, h_1, l_1, c_1, t_1, o1_1, h1_1, l1_1, c1_1, t1_1, o2_1, h2_1, l2_1, c2_1, t2_1]  = request.security(syminfo.tickerid, htf_1, [open[1], high[1], low[1], close[1], time[1], open[2], high[2], low[2], close[2], time[2], open[3], high[3], low[3], close[3], time[3]])
    TF_1 := FVG_1_Structure._ProcessData(o_1, h_1, l_1, c_1, t_1, o1_1, h1_1, l1_1, c1_1, t1_1, o2_1, h2_1, l2_1, c2_1, t2_1, HTF_1_Settings.label_text)
    FVG_1_Structure._DrawBoxes(TF_1)
